buildscript {
    dependencies {
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:0.4.1"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
    }
}

plugins {
    id "com.github.hierynomus.license" version"0.15.0"
    id 'com.github.nbaztec.coveralls-jacoco' version '1.2.15'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'net.ltgt.errorprone' version  '1.1.1'
    id 'java-library'
    id 'jacoco'
    id 'com.google.protobuf' version '0.8.11'
}

repositories {
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    mavenCentral()
    jcenter()
}

apply plugin: 'com.google.protobuf'
apply plugin: 'idea' // IntelliJ plugin to see files generated from protos
apply plugin: 'maven'
apply plugin: 'org.jruyi.thrift'
apply plugin: 'maven-publish'
apply plugin: 'com.github.sherter.google-java-format'

if (hasProperty('signing.keyId')) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

googleJavaFormat {
    toolVersion '1.5'
    include '**/*.java'
    exclude '**/generated-sources/*'
}

group = 'com.uber.cadence'
version = '3.12.5'

description = '''Uber Cadence Java Client'''

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')
    errorprone('com.google.errorprone:error_prone_core:2.3.4')

    compile group: 'com.uber.tchannel', name: 'tchannel-core', version: '0.8.30'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    compile group: 'org.apache.thrift', name: 'libthrift', version: '0.9.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.10'
    compile group: 'com.uber.m3', name: 'tally-core', version: '0.11.1'
    compile group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    compile group: 'com.cronutils', name: 'cron-utils', version: '9.2.0'
    compile group: 'io.micrometer', name: 'micrometer-core', version: '1.10.2'
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile group: 'com.auth0', name: 'java-jwt', version:'3.10.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.9'
    compile group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '2.10.0'
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.21.9'
    compile group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.35.0'

    implementation 'io.grpc:grpc-netty-shaded:1.54.2'
    implementation 'io.grpc:grpc-protobuf:1.54.2'
    implementation 'io.grpc:grpc-stub:1.54.2'

    compile("io.opentelemetry:opentelemetry-api:1.19.0")

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.googlecode.junit-toolbox', name: 'junit-toolbox', version: '2.4'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'io.grpc', name: 'grpc-testing', version: '1.54.2'
    testImplementation 'io.opentracing:opentracing-mock:0.33.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.5.1'
    testImplementation "org.mockito:mockito-inline:4.5.1" // for mocking final classes
}

license {
    header rootProject.file('license-header.txt')
    skipExistingHeaders true
    excludes(["**/*.json", "**/idls","com/uber/cadence/*.java", "com/uber/cadence/shadower/*.java"]) // config files and generated code
}

task initDlsSubmodule(type: Exec) {
    description = 'Initializes src/main/idls submodule'
    commandLine 'git', 'submodule', 'init'
}

task updateDlsSubmodule(type: Exec) {
    dependsOn initDlsSubmodule
    description = 'Updates src/main/idls submodule'
    commandLine 'git', 'submodule', 'update'
}

compileThrift {
    dependsOn updateDlsSubmodule
    verbose true
    sourceItems "${projectDir}/src/main/idls/thrift/cadence.thrift","${projectDir}/src/main/idls/thrift/shared.thrift","${projectDir}/src/main/idls/thrift/shadower.thrift"
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/idls/proto'
        }
        java {
            srcDir 'src/main'
        }
    }
}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.11.0:osx-x86_64' // no arm version available
        } else {
            artifact = 'com.google.protobuf:protoc:3.11.0'
        }
    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.28.0:osx-x86_64' // no arm version available
            } else {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.28.0'
            }
        }
    }
    generateProtoTasks {
        all().each { task -> task.dependsOn updateDlsSubmodule }
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$buildDir/generated-sources/proto"
}
idea {
    module {
        sourceDirs += file("$buildDir/generated/main/java");
        sourceDirs += file("$buildDir/generated/main/grpc");
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

compileJava {
    dependsOn 'googleJavaFormat'
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:none' << '-Xlint:deprecation' << '-Werror'
    options.errorprone.excludedPaths = '.*/generated-sources/.*'
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:none' << '-Xlint:deprecation' << '-Werror'
    options.errorprone.excludedPaths = '.*/generated-sources/.*'
}

// Generation version.properties for value to be included into the request header
task createProperties(dependsOn: processResources) {
    doLast {
        def subdir = new File("$buildDir/resources/main/com/uber/cadence/")
        if( !subdir.exists() ) {
            subdir.mkdirs()
        }
        new File("$buildDir/resources/main/com/uber/cadence/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['cadence-client-version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def ossrhUsername = hasProperty('ossrhUsername') ? property('ossrhUsername') : ''
def ossrhPassword = hasProperty('ossrhPassword') ? property('ossrhPassword') : ''

publishing {

    publications {
        // Uncomment below if you want to run "publishMavenLocal"
        maven(MavenPublication) {
            pom.withXml {
                asNode().with {
                    appendNode('packaging', 'jar')
                    appendNode('name', 'cadence-client')
                    appendNode('description', description)
                    appendNode('url', 'https://github.com/uber-java/cadence-client')
                    appendNode('scm').with {
                        appendNode('url', 'https://github.com/uber-java/cadence-client')
                        appendNode('connection', 'git@github.com:uber-java/cadence-client.git')
                    }
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'The Apache License, Version 2.0')
                            appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                        }
                    }
                    appendNode('developers').with {
                        appendNode('maxim').with {
                            appendNode('id', 'maxim')
                            appendNode('name', 'Maxim Fateev')
                            appendNode('email', 'maxim@uber.com')
                        }
                        appendNode('developer').with {
                            appendNode('id', 'meiliang')
                            appendNode('name', 'Liang Mei')
                            appendNode('email', 'meiliang@uber.com')
                        }
                    }
                }
            }
        }

        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            } else {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }
}

task registerDomain(type:JavaExec) {
    main = 'com.uber.cadence.RegisterTestDomain'
    classpath = sourceSets.test.runtimeClasspath
}

test {
    dependsOn 'registerDomain'
    dependsOn 'licenseMain'
    finalizedBy jacocoTestReport // report is always generated after tests run
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat 'full'
        // Uncomment the following line if you want to see test logs in gradlew run.
        showStandardStreams true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'cadence-client'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Uber Cadence Java Client'
                url 'https://github.com/uber-java/cadence-client'

                scm {
                    connection 'scm:git:git@github.com:uber-java/cadence-client.git/'
                    developerConnection 'scm:git:git@github.com:uber-java/cadence-client.git/'
                    url 'https://github.com/uber-java/cadence-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'maxim'
                        name 'Maxim Fateev'
                        email 'maxim@uber.com'
                    }
                    developer {
                        id 'mkol'
                        name 'Max K'
                        email 'mkol@uber.com'
                    }
                    developer {
                        id 'meiliang'
                        name 'Liang Mei'
                        email 'meiliang@uber.com'
                    }
                }
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: ['com/uber/cadence/shadower/*.class', 'com/uber/cadence/*.class'])
        }))
    }
}
