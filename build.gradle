buildscript {
    dependencies {
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:0.4.0"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.14.0"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "org.jruyi.thrift"
apply plugin: 'maven-publish'

group = 'com.uber'
version = '0.2.0-SNAPSHOT'

description = """Uber Cadence Java Client"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'com.uber.tchannel', name: 'tchannel-core', version: '0.7.6'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile(group: 'org.apache.thrift', name: 'libthrift', version: '0.9.3') {
        exclude(module: 'commons-logging')
    }
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'log4j', name: 'log4j', version: '1.2.17'
}

license {
    header rootProject.file('license-header.txt')
    mapping {
        java='SLASHSTAR_STYLE'
    }
    include 'src/**/*.java'
    strictCheck true
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        mavenLocal()
    }
}
